; Installer for Kara.
;
;   "C:\Program Files (x86)\Inno Setup 6\ISCC.exe" packaging\installer.iss
;
; Installs per user, under Local AppData, so Windows never asks for an
; administrator password. A dictation tool is not worth a UAC prompt, and asking
; for one is what makes people close the window and give up.

#define AppName        "Kara"
#define AppPublisher   "bryramirezp"
#define AppURL         "https://github.com/bryramirezp/kara"
#define AppExeName     "Kara.exe"

; Passed in by packaging/build.py, which reads __version__ out of the source so
; the version is written down in exactly one place. The fallback only matters
; when someone runs ISCC by hand.
#ifndef AppVersion
  #define AppVersion "0.3.2"
#endif

; Set by packaging/build.py --gpu. Only the file name changes: the AppId is
; deliberately the same for both, so installing one over the other upgrades in
; place rather than leaving two copies of Kara on the machine.
#ifdef GpuBuild
  #define Flavour "-GPU"
#else
  #define Flavour ""
#endif

[Setup]
AppId={{7C1B4E62-9A3D-4F58-B0E7-2D6A5F8C1E90}
AppName={#AppName}
AppVersion={#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}/issues
AppUpdatesURL={#AppURL}/releases

DefaultDirName={localappdata}\Programs\Kara
DefaultGroupName={#AppName}
DisableProgramGroupPage=yes
PrivilegesRequired=lowest
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible

OutputDir=..\dist
OutputBaseFilename=Kara-Setup{#Flavour}-{#AppVersion}
SetupIconFile=..\assets\icon.ico
UninstallDisplayIcon={app}\{#AppExeName}
WizardStyle=modern
Compression=lzma2/max
SolidCompression=yes
VersionInfoVersion={#AppVersion}

[Languages]
Name: "english";             MessagesFile: "compiler:Default.isl"
Name: "spanish";              MessagesFile: "compiler:Languages\Spanish.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "french";               MessagesFile: "compiler:Languages\French.isl"
Name: "german";               MessagesFile: "compiler:Languages\German.isl"
Name: "italian";              MessagesFile: "compiler:Languages\Italian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; Flags: unchecked
Name: "startup";     Description: "Start Kara when Windows starts"

[InstallDelete]
#ifndef GpuBuild
; The two installers share an AppId on purpose, so that either upgrades the
; other in place rather than leaving two copies of Kara on the machine. The
; price is this: Inno only removes what it installed itself, so putting the
; processor build over the GPU one would leave 925 MB of CUDA libraries sitting
; in the install folder, outliving even the uninstaller. PyInstaller's onedir
; layout puts them under _internal.
Type: filesandordirs; Name: "{app}\_internal\nvidia"
#endif

[Files]
Source: "..\dist\Kara\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs

[Icons]
Name: "{group}\{#AppName}";            Filename: "{app}\{#AppExeName}"
Name: "{group}\Uninstall {#AppName}";  Filename: "{uninstallexe}"
Name: "{autodesktop}\{#AppName}";      Filename: "{app}\{#AppExeName}"; Tasks: desktopicon
Name: "{userstartup}\{#AppName}";      Filename: "{app}\{#AppExeName}"; Tasks: startup

[Run]
Filename: "{app}\{#AppExeName}"; Description: "{cm:LaunchProgram,{#AppName}}"; \
    Flags: nowait postinstall skipifsilent

[UninstallDelete]
; The app is a tray program: it can still be running when the uninstaller
; starts, and a leftover shortcut would point at nothing.
Type: files; Name: "{userstartup}\{#AppName}.lnk"

[Code]
function KaraUILang(): String;
begin
  case ActiveLanguage of
    'spanish': Result := 'es';
    'brazilianportuguese': Result := 'pt';
    'french': Result := 'fr';
    'german': Result := 'de';
    'italian': Result := 'it';
  else
    Result := 'en';
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  SettingsDir, SettingsFile: String;
begin
  if CurStep = ssPostInstall then begin
    SettingsDir := ExpandConstant('{localappdata}\Kara');
    SettingsFile := SettingsDir + '\settings.json';
    // Only seed on a first install. An upgrade or repair must never overwrite
    // a UI language the user already chose from inside the app -- that would
    // silently undo Settings -> APP LANGUAGE every time a new version installs.
    if not FileExists(SettingsFile) then begin
      ForceDirectories(SettingsDir);
      SaveStringToFile(SettingsFile,
        '{"ui_language": "' + KaraUILang() + '"}', False);
    end;
  end;
end;
